pkgname=mate-notification-daemon
pkgver=2011.9.26
pkgrel=1
pkgdesc="Notification daemon for MATE"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
depends=('libmatenotify' 'libwnck')
makedepends=('intltool')
url="http://matsusoft.com.ar/projects"
groups=('mate')
options=('!libtool' '!emptydirs')
install=mate-notification-daemon.install
source=(http://sourceforge.net/projects/matede/files/${pkgver}/${pkgname}.tar.gz/download)
sha256sums=('61f473628618089d87a01b34934756b5fe0f05c252f8db24d2726595cc30ad59')

build() {
	cd "${srcdir}/${pkgname}"

	automake
	autoreconf -i --force
	intltoolize --copy --force --automake --debug

	./configure --prefix=/usr --sysconfdir=/etc \
		--localstatedir=/var --disable-static \
		--libexecdir=/usr/lib/mate-notification-daemon \
		--disable-schemas-install || return 1

	make || return 1
}

package() {
	cd "${srcdir}/${pkgname}"

	make MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

	install -m755 -d "${pkgdir}/usr/share/mateconf/schemas"
	mateconf-merge-schema "${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas" --domain ${pkgname} ${pkgdir}/etc/mateconf/schemas/*.schemas || return 1
	rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas
}
