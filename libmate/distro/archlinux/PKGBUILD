pkgname=libmate
pkgver=2011.9.26
pkgrel=1
pkgdesc="Common libraries for MATE"
arch=('i686' 'x86_64')
license=('LGPL')
depends=( 'ttf-freefont' 'mate-conf')
makedepends=('mate-vfs' 'libmatecomponent' 'gvfs' 'libcanberra')
options=('!emptydirs' '!libtool')
url="http://matsusoft.com.ar/projects"
groups=('mate')
install=libmate.install

# mirrors
# http://cptl.org/mate/${pkgname}.tar.gz
# http://hax0r.se/mate/${pkgname}.tar.gz
# http://matsusoft.com.ar/uploads/gnu-linux/mate/${pkgname}.tar.gz

source=(http://sourceforge.net/projects/matede/files/${pkgver}/${pkgname}.tar.gz/download)
sha256sums=('171285849ba18c8bed04e427c780bb07323043501aac6d2424dcbc52ff4c2718')

build() {
	cd "${srcdir}/${pkgname}"
	./configure --prefix=/usr --sysconfdir=/etc \
		--localstatedir=/var --disable-static \
		--libexecdir=/usr/lib/libmate --enable-canberra \
		--disable-schemas-install || return 1
	# --disable-esd
	make || return 1
}

package() {
	cd "${srcdir}/${pkgname}"

	make MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1  DESTDIR="${pkgdir}" install || return 1

	install -m755 -d "${pkgdir}/usr/share/mateconf/schemas"
	mateconf-merge-schema "${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas" --domain ${pkgname} ${pkgdir}/etc/mateconf/schemas/*.schemas || return 1
	rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas
}
