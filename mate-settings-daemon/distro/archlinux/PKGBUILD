pkgname=mate-settings-daemon
pkgver=2011.9.26
pkgrel=1
pkgdesc="The MATE Settings daemon"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libmatekbd' 'mate-desktop' 'libmatenotify' 'gstreamer0.10-base' 'hicolor-icon-theme')
makedepends=('intltool' 'pkgconfig')
options=('!emptydirs' '!libtool')
url="http://matsusoft.com.ar/projects"
install=mate-settings-daemon.install
groups=('mate')
source=(http://sourceforge.net/projects/matede/files/${pkgver}/${pkgname}.tar.gz/download)
sha256sums=('fc9b1b107d92ea5037355a178e8b7ba1c86c12d92b62a149d9c121fed075edd9')

build() {
	cd "$srcdir/$pkgname"
	libtoolize --force || return 1
	aclocal || return 1
	autoconf || return 1
	automake || return 1

	# Remove --disable-pulse if you want pulseaudio support
	./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
		--libexecdir=/usr/bin/${pkgname} \
		--disable-static --disable-pulse --disable-schemas-install \
		--enable-polkit || return 1

	make
}

package() {
	cd "${srcdir}/${pkgname}"

	make DESTDIR="${pkgdir}" MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install || return 1

	install -d -m755 "${pkgdir}/usr/share/mateconf/schemas"
	mateconf-merge-schema "${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas" --domain ${pkgname} ${pkgdir}/etc/mateconf/schemas/*.schemas || return 1
	rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas
}
