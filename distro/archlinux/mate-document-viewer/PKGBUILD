pkgname=mate-document-viewer
pkgver=0.0.0
pkgrel=2
pkgdesc="Simply a document viewer"
url="http://projects.gnome.org/evince/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk2' 'libspectre' 'gsfonts' 'poppler-glib' 'djvulibre' 'mate-icon-theme'
         't1lib' 'libmatekeyring' 'desktop-file-utils') #  'dconf' 'gsettings-desktop-schemas'
makedepends=('mate-doc-utils' 'mate-file-manager' 'texlive-bin' 'intltool' 'gobject-introspection')
optdepends=('texlive-bin: DVI support')
groups=('mate-extra')
install=mate-document-viewer.install
options=('!libtool' '!emptydirs')
source=(http://matsusoft.com.ar/uploads/gnu-linux/mate/${pkgname}.tar.gz)
sha256sums=('0e5e5eb4652c830f33b620d82ea1c8e47effe84c9f86609de613bee8fb509842')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	autoreconf -fi

	./configure --prefix=/usr   --sysconfdir=/etc \
		--localstatedir=/var    --libexecdir=/usr/lib/${pkgname} \
		--disable-static        --enable-caja \
		--enable-pdf            --enable-tiff \
		--enable-djvu           --enable-dvi \
		--enable-t1lib          --enable-comics \
		--disable-scrollkeeper \
		--enable-introspection || return 1
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

	install -m755 -d "${pkgdir}/usr/share/mateconf/schemas"
	mateconf-merge-schema "${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas" --domain ${pkgname} ${pkgdir}/etc/mateconf/schemas/*.schemas
	rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas
}
