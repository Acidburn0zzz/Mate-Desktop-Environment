pkgname=mate-file-manager-open-terminal
pkgver=2011.08.31
pkgrel=1
pkgdesc="An extension for opening terminals in arbitrary local paths"
arch=('i686' 'x86_64')
url="http://manny.cluecoder.org/packages/nautilus-open-terminal"
license=('GPL')
depends=('mate-file-manager')
makedepends=('pkgconfig' 'intltool')
install=mate-file-manager-open-terminal.install
options=('!libtool')
group=('mate-extras')
source=(http://sourceforge.net/projects/matede/files/${pkgver}/${pkgname}.tar.gz/download)
sha256sums=('ca8006db87798d8720d7e9aa494ef380bf76d1f5acb204ff4f2c2b02785ecb51')

build() {
	cd "$srcdir/$pkgname"

	./configure --prefix=/usr || return 1

	#fix non-standard directory
	sed -i 's|${prefix}/etc|/etc|' Makefile

	make || return 1
}

package() {
	cd "$srcdir/$pkgname"
	make MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir" install || return 1

	install -m755 -d ${pkgdir}/usr/share/mateconf/schemas

	mateconf-merge-schema ${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas --domain ${pkgname} ${pkgdir}/etc/mateconf/schemas/*.schemas || return 1
	rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas
}
