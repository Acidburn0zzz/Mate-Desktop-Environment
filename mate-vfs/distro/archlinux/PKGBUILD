pkgname=mate-vfs
pkgver=2011.08.31
pkgrel=1
pkgdesc="The MATE Virtual File System"
arch=(i686 x86_64)
license=('LGPL')
depends=('fam' 'mate-conf' 'bzip2' 'avahi' 'smbclient' 'mate-mime-data') #  'krb5'   'gnutls'
makedepends=('pkgconfig' 'intltool' 'gtk-doc' 'mate-common')
options=('!emptydirs')
url="http://matsusoft.com.ar/projects"
groups=('mate')
install=mate-vfs.install
source=(http://sourceforge.net/projects/matede/files/${pkgver}/${pkgname}.tar.gz/download)
sha256sums=('bdf15c16df4f8431e255748600ce872e829c1e21970ccc51b280d2404e31d4da')

build() {
	cd "${srcdir}/${pkgname}"
	#Fix build with new gnutls
	#patch -Np1 -i "${srcdir}/gnutls-config.patch"
	libtoolize --force
	gtkdocize
	aclocal
	autoconf
	automake
	./configure --prefix=/usr --sysconfdir=/etc \
		--localstatedir=/var --disable-static \
		--libexecdir=/usr/lib/${pkgname} \
		--enable-samba --disable-hal \
		--enable-avahi --disable-howl \
		--enable-openssl
		#--disable-openssl --enable-gnutls
	make
}

package() {
	cd "${srcdir}/${pkgname}"
	make MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

	install -d -m755 "${pkgdir}/usr/share/mateconf/schemas"
	mateconf-merge-schema "${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas" --domain ${pkgname} ${pkgdir}/etc/mateconf/schemas/*.schemas
	rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas
}
